set(LOCAL_TARGET_NAME ${PROJECT_NAME}_LibB)

set(SOURCE_FILES 
	src/MathLibrary.cpp
)

set(API_HEADERS 
	Include/MathLibrary.h
)

add_library( ${LOCAL_TARGET_NAME} ${SOURCE_FILES} ${API_HEADERS})

set_target_properties(${LOCAL_TARGET_NAME} PROPERTIES DEBUG_POSTFIX "d") 

target_include_directories(${LOCAL_TARGET_NAME} PUBLIC 
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/Include>
)

include(GenerateExportHeader) 

string(TOUPPER ${LOCAL_TARGET_NAME} LOCAL_TARGET_NAME_UPPERCASE)

GENERATE_EXPORT_HEADER( ${LOCAL_TARGET_NAME}
             BASE_NAME ${LOCAL_TARGET_NAME_UPPERCASE}
             EXPORT_MACRO_NAME ${LOCAL_TARGET_NAME_UPPERCASE}_MATHLIBRARY_API
             EXPORT_FILE_NAME ${LOCAL_TARGET_NAME}_Export.h
             STATIC_DEFINE ${LOCAL_TARGET_NAME_UPPERCASE}_BUILT_AS_STATIC
)

target_precompile_headers(${LOCAL_TARGET_NAME}
  PRIVATE
    <utility>
	<limits.h>
)

install(TARGETS ${LOCAL_TARGET_NAME}
    EXPORT ${PROJECT_NAME}Targets
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

install (FILES ${API_HEADERS} ${CMAKE_CURRENT_BINARY_DIR}/${LOCAL_TARGET_NAME}_Export.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
